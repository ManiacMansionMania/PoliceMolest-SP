// 1. Stock (Police Molest)
bool bFirst = true;

/****************************
*   Raum-Funktionen
****************************/
function room_Load()
{
  aMusic54.Play();
	String seperator = String.Format("\\ **** erster Stock");
	Door[eDoorNumZelle].Init(oTuer, hTuer);
	oGlas.Clickable = false;
	oKlappe.Clickable = false;
	oFahrstuhltuer.Clickable = false;

	if ( player.PreviousRoom == 15 ) // Spieler kommt aus dem Fahrstuhl
	{
		player.x	= -40;
		player.y	= 133;
	}
}

function room_AfterFadeIn()
{
	if ( player.PreviousRoom == 15 ) // Spieler kommt aus dem Fahrstuhl
	{
		StartCutscene(eSkipESCOrRightButton);
			PlaySound(90);
			oFahrstuhltuer.SetView(56, 0);
			oFahrstuhltuer.Animate(0, 2, eOnce, eBlock, eForwards);
			Wait(50);

			player.Walk(60, 133, eBlock, eAnywhere);
			Wait(20);

			oFahrstuhltuer.Animate(2, 2, eOnce, eBlock, eForwards);
			Wait(40);
		EndCutscene();
		
		gMaingui.Visible	= true;
		gAction.Visible		= true;
		Mouse.Visible			= true;
	}

#ifdef STARTERPACK_INTRO
	if ( bFirst )
	{
		bFirst = false;
		Wait(20);
		player.Say("Hier im 1. Stock des Reviers werden festgenommene Personen für die kriminaltechnische Untersuchung vorbereitet.");
		player.Say("Hinter dem Schalter erfüllt Officer Sunshine seit Jahren seine Pflicht.");
		player.Say("Seinem Teint hat das eher geschadet.");
	}
#endif
}


/****************************
*   Tür-Hotspot-Funktionen
****************************/
function hTuer_AnyClick()
{
	if ( Door[eDoorNumZelle].Click() == 0 ) Unhandled();
}


/****************************
*   Objekt-Funktionen
****************************/
function oKlappe_AnyClick()
{

}


/****************************
*   Hotspot-Funktionen
****************************/
function hKnopf_AnyClick()
{
	if ( UsedAction(eActWalkTo) )
	{
		if ( any_click_move(60, 123, eDirectionUp) )
		{
		}
	}
	else if ( UsedAction(eActPush) || UsedAction(eActUse) )
	{
		if ( any_click_move(60, 123, eDirectionUp) )
		{
			Wait(20);
			gMaingui.Visible = false;
			gAction.Visible = false;
			StartCutscene(eSkipESCOrRightButton);
				PlaySound(83);
				Wait(20);
				PlayAmbientSound(1, 170, 100, 0, 0);

				while ( gi_Stockwerk < 1 )
				{
					gi_Stockwerk++;
					Wait(40);
				}

				StopAmbientSound(1);
				PlaySound(90);
				oFahrstuhltuer.SetView(56, 1, 0);
				oFahrstuhltuer.Animate(1, 0, eOnce, eBlock, eForwards); // Fahrstuhltür auf
				Wait(20);
			
				player.Walk(43, 133, eBlock, eAnywhere);
				player.FaceDirection(eDirectionLeft);
				Wait(40);
				player.Walk(-40, 133, eBlock, eAnywhere);
				oFahrstuhltuer.Animate(1, 0, eOnce, eBlock, eBackwards); // Fahrstuhltür zu
				Wait(40);
			EndCutscene();
			player.ChangeRoom(15); // Spieler in den Fahrstuhl schicken
		}
	}
	else
	{
		player.FaceDirection(eDirectionLeft);
		Unhandled();
	}
}

function hFahrstuhltuer_AnyClick()
{
	if (any_click_move(57, 130, eDirectionLeft))
	{
		if (UsedAction(eActWalkTo))
		{
		}
		else if (UsedAction(eActLookAt))
		{
			Wait(5);
			player.Say("Diese Fahrstuhltüren sehen in jedem Stockwerk fast identisch aus ...");
		}
		else if (UsedAction(eActPickUp))
		{
			Wait(5);
			player.Say("");
		}
		else if (UsedAction(eActUse) || UsedAction(eActOpen))
		{
			Wait(5);
			player.Say("Dazu muss ich das Panel benutzen.");
		}
		else if (UsedAction(eActClose))
		{
			Wait(5);
			player.Say("Die Tür ist schon zu.");
		}
		else if (UsedAction(eActPull) || UsedAction(eActPush))
		{
			Wait(5);
			player.Say("Das funktioniert nicht.");
		}
		else if (UsedAction(eActUseInventory))
		{
			Wait(5);
			player.Say("Nicht damit.");
		}
		else Unhandled();
	}
}

function hKlappe_AnyClick()
{
	if (any_click_move(132, 124, eDirectionUp))
	{
		if (UsedAction(eActWalkTo))
		{
		}
		else if (UsedAction(eActLookAt))
		{
			Wait(5);
			player.Say("Eine Klappe, damit wir auch Sachen einlagern können. Beweismittel und so Sachen.");
		}
		else if (UsedAction(eActPickUp) || UsedAction(eActUse) || UsedAction(eActOpen) || UsedAction(eActPull))
		{
			Wait(5);
			PlaySound(1);
			oKlappe.Visible = true;
			Wait(20);
			cSunshine.Say("Haben Sie was zum Einlagern?");
			player.Say("Nein, momentan nicht.");
			cSunshine.Say("Dann lassen Sie Ihre Finger davon!"); 
			Wait(20);
			PlaySound(1);
			oKlappe.Visible = false;
		}
		else if (UsedAction(eActClose) || UsedAction(eActPush))
		{
			Wait(5);
			player.Say("Die Klappe ist schon zu.");
		}
		else if (UsedAction(eActUseInventory))
		{
			Wait(5);
			player.Say("Das will ich nicht da reintun.");
		}
		else Unhandled();
	}
}


/****************************
*   Regionen-Funktionen
****************************/


