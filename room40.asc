// Parkplatz (Police Molest)

/****************************
*   Raum-Funktionen
****************************/
function room_Load()
{
	String seperator = String.Format("\\ **** Parkplatz");
	StopMusic();
	PlayAmbientSound(1, 108, 100, 1, 100);
}


/****************************
*   Tür-Hotspot-Funktionen
****************************/



/****************************
*   Objekt-Funktionen
****************************/
function oWagen73_AnyClick()
{
	if (any_click_move(47, 143, eDirDown))
	{
		if (UsedAction(eActWalkTo))
		{
		}
		else if (UsedAction(eActLookAt))
		{
			Wait(5);
			if ( ! cSimpkins.SayIfPlayer("Das ist der mir zugewiesene Streifenwagen.") )
			{
				player.Say("Wagen 73.");
			}
		}
		else if (UsedAction(eActPickUp))
		{
			Wait(5);
			player.Say("Das ist mir zu schwer.");
		}
		else if (UsedAction(eActUse) || UsedAction(eActOpen) || UsedAction(eActPull))
		{
			Wait(5);
			if ( player.InventoryQuantity[iFahrzeugschluessel.ID] )
			{
				PlaySound(50);
				Wait(40);
				player.Walk(47, 163, eBlock, eAnywhere);
				Wait(41);
				player.ChangeRoom(41);
				PlaySound(78); 
			}
			else
			{
				player.Say("Ohne den Schlüssel komme ich nicht weiter.");
			}
		}
		else if (UsedAction(eActClose) || UsedAction(eActPush))
		{
			Wait(5);
			player.Say("Der ist schon zu.");
		}
		else if (UsedAction(eActUseInventory))
		{
			Wait(5);
			if ( player.ActiveInventory == iFahrzeugschluessel )
			{
				PlaySound(50);
				Wait(40);
				player.Walk(47, 163, eBlock, eAnywhere);
				Wait(41);
				player.ChangeRoom(41);
				PlaySound(78);
			}
			else
			{
				player.Say("Ohne den Schlüssel komme ich nicht weiter.");
			}
		}
		else Unhandled();
	}
}

function oWagen78_AnyClick()
{
	if (any_click_move(47, 143, eDirUp))
	{
		if (UsedAction(eActWalkTo))
		{
		}
		else if (UsedAction(eActLookAt))
		{
			Wait(5);
			player.Say("Wagen 78.");
		}
		else if (UsedAction(eActPickUp))
		{
			Wait(5);
			player.Say("Der ist mir zu schwer.");
		}
		else if (UsedAction(eActUse) || UsedAction(eActOpen) || UsedAction(eActPull))
		{
			Wait(5);
			player.Say("Das ist nicht meiner.");
		}
		else if (UsedAction(eActClose) || UsedAction(eActPush))
		{
			Wait(5);
			player.Say("Die Türen sind ordnungsgemäß verschlossen.");
		}
		else if (UsedAction(eActUseInventory))
		{
			Wait(5);
			player.Say("Das bringt nichts.");
		}
		else Unhandled();
	}
}


/****************************
*   Hotspot-Funktionen
****************************/
function hEingang_AnyClick()
{
	player.FaceDirection(eDirRight);

	if (UsedAction(eActWalkTo))
	{
		any_click_move(320, 138, eDirRight);
	}
	else if (UsedAction(eActLookAt))
	{
		Wait(5);
		player.Say("Da geht es zum Präsidiums-Eingang.");
	}
	else
	{
		Unhandled();
	}
}


/****************************
*   Regionen-Funktionen
****************************/
function region1_WalksOnto()
{
	player.EnterRoom(16, 10, 135, eDirRight);
}
