// Fahrstuhl (Police Molest)
#define TARGET_LEVEL_4					4
#define TARGET_LEVEL_3					3
#define TARGET_LEVEL_2					2
#define TARGET_LEVEL_1					1
#define TARGET_LEVEL_GROUND			0
#define TARGET_LEVEL_KELLER1		-1
#define TARGET_LEVEL_KELLER2		-2


/****************************
*   Raum-Funktionen
****************************/
function room_FirstLoad()
{
	String seperator = String.Format("\\ **** Fahrstuhl");
	gi_Stockwerk = 0; // im Erdgeschoß anfangen
}

function room_Load()
{
	if ( player.PreviousRoom == 35 )
	{
	// vor Millers Büro
		SetMusicVolume(-3);
	}
	else
	{
		SetMusicVolume(-2);
	}
}


/****************************
*   Tür-Hotspot-Funktionen
****************************/



/****************************
*   Objekt-Funktionen
****************************/



/****************************
*   Hotspot-Funktionen
****************************/
int Fahrstuhl(int l_ObjektX, int l_ObjektY, int i_TargetLevel, 
							int l_NewRoom, bool b_NeedKey, InventoryItem *i_InvItem)
{
	StartCutscene(eSkipESCOrRightButton);
		if ( b_NeedKey && i_InvItem != null && player.InventoryQuantity[i_InvItem.ID] ) 
		{
				Wait(10);
				PlaySound(4);
				oSchluessel.Visible = true;
				Wait(30);
				oSchluessel.Graphic = 450;
				PlaySound(46);
				Wait(20);
				oSchluessel.Graphic = 451;
				Wait(30);
		}
		
		if ( (b_NeedKey && i_InvItem != null && player.InventoryQuantity[i_InvItem.ID]) || !b_NeedKey )
		{
			oKnopf.X = l_ObjektX;
			oKnopf.Y = l_ObjektY;
			oKnopf.Visible = true;
			PlaySound(83);
			Wait(20);
			oKnopf.Visible = false;
			
			PlayAmbientSound(1, 170, 200, 0, 0);
			
			while ( gi_Stockwerk < i_TargetLevel )
			{
				gi_Stockwerk++;
				ShakeScreenBackground(10, 1, 40); 
				Wait(40);
			}

			while ( gi_Stockwerk > i_TargetLevel )
			{
				gi_Stockwerk--;
				ShakeScreenBackground(10, 1, 40); 
				Wait(40);
			}
			
			StopAmbientSound(1);
			Wait(20);
		}

		if ( b_NeedKey && i_InvItem != null && player.InventoryQuantity[i_InvItem.ID] ) 
		{
				oSchluessel.Graphic = 450;
				PlaySound(46);
				Wait(20);
				oSchluessel.Graphic = 449;
				Wait(30);
				oSchluessel.Visible = false;
				PlaySound(4);
				Wait(30);
		}

		if ( b_NeedKey && i_InvItem != null && player.InventoryQuantity[i_InvItem.ID] == 0 )
		{
			player.Say("Für das Stockwerk brauche ich einen besonderen Sicherheitsschlüssel, ...");
			player.Say("den ich leider nicht dabei habe.");
		}

		if ( (b_NeedKey && i_InvItem != null && player.InventoryQuantity[i_InvItem.ID]) || !b_NeedKey )
		{
			player.EnterRoom(l_NewRoom, -40, 137, eDirRight);
		}
	EndCutscene();
}

function hErdgeschoss_AnyClick()
{
	//Fahrstuhl(int l_ObjektX, int l_ObjektY, int i_TargetLevel, 
	//						int l_NewRoom, bool b_NeedKey, InventoryItem *i_InvItem)
	return Fahrstuhl(144, 94, TARGET_LEVEL_GROUND, 17, false, null);
}

function hKeller1_AnyClick()
{
	return Fahrstuhl(144, 110, TARGET_LEVEL_KELLER1, 18, true, iFahrstuhlsch);
}

function hKeller2_AnyClick()
{
	return Fahrstuhl(144, 124, TARGET_LEVEL_KELLER2, 24, true, iFahrstuhlsch);
}

function hStock1_AnyClick()
{
	return Fahrstuhl(144, 80, TARGET_LEVEL_1, 30, false, null);
}

function hStock2_AnyClick()
{
	return Fahrstuhl(144, 66, TARGET_LEVEL_2, 35, false, null);
}

function hStock3_AnyClick()
{
	player.Say("Warum sollte ich in dieses Stockwerk fahren?");
}

function hStock4_AnyClick()
{
	return hStock3_AnyClick();
}


/****************************
*   Regionen-Funktionen
****************************/
