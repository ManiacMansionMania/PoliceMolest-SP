// Meteorkrater (Police Molest)
int l_SpeedX;
int l_SpeedY;

/****************************
*   Raum-Funktionen
****************************/
function room_Load()
{
	String seperator = String.Format("\\ **** Meteorkrater");
	player.PlaceCharacter(-27, 127, eDirectionRight);
	PlayAmbientSound(1, 96, 150, 0, 0);
	Mouse.Visible = false;
	oKrater.SetView(59, 0);
	oKrater.Animate(0, 0, eRepeat, eNoBlock);
}

function room_AfterFadeIn()
{
	oWagen.SetView(59, 1);
	oWagen.Animate(1, 0, eRepeat, eNoBlock);
	Wait(90);
	PlaySound(102);

	oWagen.Visible = true;
	oWagen.Move(-148, 140, -2, eBlock, eAnywhere);
	oWagen.SetView(59, 1);

	Wait(30);
	PlaySound(50);
	Wait(30);

	player.PlaceCharacter(-27, 127, eDirectionRight);
	player.Transparency = 0;
	
	Wait(30);
	//PlaySound(78);
	
	player.Walk(64, 120, eBlock, eAnywhere);
	player.FaceDirection(eDirectionRight);
	
	player.Say("Aha!");
	player.Say("Hier scheint dieser Meteroit gelandet zu sein.");

	player.Walk(123, 132, eBlock, eAnywhere);
	player.FaceDirection(eDirectionRight);

	player.Say("Merkwürdig.");
	player.Say("Da unten ist gar nichts zu sehen ...");
	Wait(30);
	player.FaceDirection(eDirectionUp);

	player.Say("Da ist die alte Edison-Villa.");
	player.Say("Die müssten eigentlich etwas bemerkt haben.");
	player.Walk(93, 108, eBlock, eAnywhere);
	player.FaceDirection(eDirectionUp);

	StartCutscene(eSkipESCOrRightButton);
		l_SpeedX = player.WalkSpeedX;
		l_SpeedY = player.WalkSpeedY;
		player.SetWalkSpeed(-1, -1);
		player.Walk(187, 95, eBlock, eAnywhere);
	EndCutscene();

	player.FaceDirection(eDirectionUp);
	player.ChangeRoom(53, 350, 133, eDirectionLeft);
}

function room_Leave()
{
	player.SetWalkSpeed(l_SpeedX, l_SpeedY);
}


/****************************
*   Tür-Hotspot-Funktionen
****************************/



/****************************
*   Objekt-Funktionen
****************************/
function oKrater_AnyClick()
{
	if (any_click_move(112, 130, eDirectionRight))
	{
		if (UsedAction(eActWalkTo))
		{
		}
		else if (UsedAction(eActLookAt))
		{
			Wait(5);
			player.Say("Ein Krater.");
		}
		else if (UsedAction(eActPickUp))
		{
			Wait(5);
			player.Say("Ein Loch aufheben?");
		}
		else if (UsedAction(eActUse))
		{
			Wait(5);
			player.Say("Ich könnte es zufüllen.");
		}
		else if (UsedAction(eActOpen) || UsedAction(eActClose)
					|| UsedAction(eActPull) || UsedAction(eActPush))
		{
			Wait(5);
			player.Say("Das geht mit einem Loch nicht.");
		}
		else if (UsedAction(eActUseInventory))
		{
			Wait(5);
			player.Say("Das will ich nicht in das Loch werfen.");
		}
		else Unhandled();
	}
}

function oWagen_AnyClick()
{
	if (any_click_move(18, 119, eDirectionDown))
	{
		if (UsedAction(eActWalkTo))
		{
		}
		else if (UsedAction(eActLookAt))
		{
			Wait(5);
			player.Say("Wagen 73.");
		}
		else if (UsedAction(eActPickUp))
		{
			Wait(5);
			player.Say("Der wigt ca. 1 Tonne. Also nein.");
		}
		else if (UsedAction(eActUse))
		{
			Wait(5);
			player.Say("Dazu brauche ich den Schlüssel.");
		}
		else if (UsedAction(eActOpen) || UsedAction(eActClose)
					|| UsedAction(eActPull) || UsedAction(eActPush))
		{
			Wait(5);
			player.Say("Nein.");
		}
		else if (UsedAction(eActUseInventory))
		{
			Wait(5);
			if ( player.ActiveInventory == iFahrzeugschluessel )
			{
				player.Say("Ja. Dann wollen wir mal.");
				player.Walk(11, 115, eBlock, eWalkableAreas);
				Wait(7);
				player.FaceDirection(eDirectionDown);
				Wait(10);
				player.ChangeRoom(41);
			}
			else
			{
				player.Say("Ich brauche meinen Fahrzeugschlüssel.");
			}
		}
		else Unhandled();
	}
}


/****************************
*   Hotspot-Funktionen
****************************/
function hVilla_AnyClick()
{
	if (UsedAction(eActWalkTo))
	{
		if (any_click_move(188, 94, eDirectionUp))
		{
			Wait(5);
			player.ChangeRoom(53, 292, 138, eDirectionLeft);
		}
	}
	else if (any_click_move(106, 106, eDirectionUp))
	{
		if (UsedAction(eActLookAt))
		{
			Wait(5);
			if ( player.IsEdison() )
			{
				player.Say("Da ist unser schönes Anwesen.");
				player.Say("Die Leute aus der Stadt nennen es 'Maniac Mansion'.");
				Wait(80);
				player.FaceDirection(eDirectionDown);
				player.Say("Warum nur?");
			}
			else
			{
				player.Say("Die Edison Villa.");
			}
		}
		else if (UsedAction(eActPickUp))
		{
			Wait(5);
			player.FaceDirection(eDirectionDown);
			Wait(20);
			player.Say("Du spielst nicht oft Adventures?");
		}
		else if (UsedAction(eActUse))
		{
			Wait(5);
			player.Say("Dazu brauche ich nur hin zu laufen.");
		}
		else if (UsedAction(eActOpen) || UsedAction(eActClose)
					|| UsedAction(eActPull) || UsedAction(eActPush))
		{
			Wait(5);
			player.Say("So nicht.");
		}
		else if (UsedAction(eActUseInventory))
		{
			Wait(5);
			player.Say("So weit kann ich nicht werfen.");
		}
		else Unhandled();
	}
}


/****************************
*   Regionen-Funktionen
****************************/
